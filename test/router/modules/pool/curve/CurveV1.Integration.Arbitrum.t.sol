// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import {IntegrationUtils} from "../../../../utils/IntegrationUtils.sol";

import {LinkedPool} from "../../../../../contracts/router/LinkedPool.sol";
import {IndexedToken, CurveV1Module} from "../../../../../contracts/router/modules/pool/curve/CurveV1Module.sol";

import {IERC20} from "@openzeppelin/contracts-4.5.0/token/ERC20/IERC20.sol";

contract CurveV1ModuleArbTestFork is IntegrationUtils {
    LinkedPool public linkedPool;
    CurveV1Module public curveV1Module;

    // 2023-07-03
    uint256 public constant ARB_BLOCK_NUMBER = 107596120;

    // Curve V1 USDC/USDT pool (2pool) on Arbitrum
    address public constant CURVE_V1_2POOL = 0x7f90122BF0700F9E7e1F688fe926940E8839F353;

    // Native USDT on Arbitrum
    address public constant USDT = 0xFd086bC7CD5C481DCC9C85ebE478A1C0b69FCbb9;

    // Bridged USDC on Arbitrum
    address public constant USDC_E = 0xFF970A61A04b1cA14834A43f5dE4533eBDDB5CC8;

    address public user;

    constructor() IntegrationUtils("arbitrum", "CurveV1Module", ARB_BLOCK_NUMBER) {}

    function afterBlockchainForked() public override {
        curveV1Module = new CurveV1Module();
        linkedPool = new LinkedPool(USDC_E, address(this));
        user = makeAddr("User");

        vm.label(CURVE_V1_2POOL, "CurveV12Pool");
        vm.label(USDT, "USDT");
        vm.label(USDC_E, "USDC.e");
    }

    // ═══════════════════════════════════════════════ TESTS: VIEWS ════════════════════════════════════════════════════

    function testGetPoolTokens() public {
        address[] memory tokens = curveV1Module.getPoolTokens(CURVE_V1_2POOL);
        assertEq(tokens.length, 2);
        assertEq(tokens[0], USDC_E);
        assertEq(tokens[1], USDT);
    }

    // ══════════════════════════════════════════════ TESTS: ADD POOL ══════════════════════════════════════════════════

    function addPool() public {
        linkedPool.addPool({nodeIndex: 0, pool: CURVE_V1_2POOL, poolModule: address(curveV1Module)});
    }

    function testAddPool() public {
        addPool();
        assertEq(linkedPool.getToken(0), USDC_E);
        assertEq(linkedPool.getToken(1), USDT);
    }

    // ════════════════════════════════════════════════ TESTS: SWAP ════════════════════════════════════════════════════

    function swap(
        uint8 tokenIndexFrom,
        uint8 tokenIndexTo,
        uint256 amount
    ) public returns (uint256 amountOut) {
        vm.prank(user);
        amountOut = linkedPool.swap({
            nodeIndexFrom: tokenIndexFrom,
            nodeIndexTo: tokenIndexTo,
            dx: amount,
            minDy: 0,
            deadline: type(uint256).max
        });
    }

    function testSwapFromUSDCtoUSDT() public {
        addPool();
        uint256 amount = 100 * 10**6;
        prepareUser(USDC_E, amount);
        uint256 expectedAmountOut = linkedPool.calculateSwap({nodeIndexFrom: 0, nodeIndexTo: 1, dx: amount});
        uint256 amountOut = swap({tokenIndexFrom: 0, tokenIndexTo: 1, amount: amount});
        assertGt(amountOut, 0);
        assertEq(amountOut, expectedAmountOut);
        assertEq(IERC20(USDC_E).balanceOf(user), 0);
        assertEq(IERC20(USDT).balanceOf(user), amountOut);
    }

    function testSwapFromUSDTtoUSDC() public {
        addPool();
        uint256 amount = 100 * 10**6;
        prepareUser(USDT, amount);
        uint256 expectedAmountOut = linkedPool.calculateSwap({nodeIndexFrom: 1, nodeIndexTo: 0, dx: amount});
        uint256 amountOut = swap({tokenIndexFrom: 1, tokenIndexTo: 0, amount: amount});
        assertGt(amountOut, 0);
        assertEq(amountOut, expectedAmountOut);
        assertEq(IERC20(USDT).balanceOf(user), 0);
        assertEq(IERC20(USDC_E).balanceOf(user), amountOut);
    }

    function testPoolSwapRevertsWhenDirectCall() public {
        vm.expectRevert("Not a delegate call");
        curveV1Module.poolSwap({
            pool: CURVE_V1_2POOL,
            tokenFrom: IndexedToken({index: 0, token: USDC_E}),
            tokenTo: IndexedToken({index: 1, token: USDT}),
            amountIn: 100 * 10**6
        });
    }

    function prepareUser(address token, uint256 amount) public {
        deal(token, user, amount);
        vm.prank(user);
        IERC20(token).approve(address(linkedPool), amount);
    }
}
